_TEXT    SEGMENT

FUNC_PROLOGUE macro
	push	r15
	mov		r15, rsp
	and		rsp, -10h
	sub		rsp, 0C0h
	movaps	[rsp + 0B0h], xmm0
	movaps	[rsp + 0A0h], xmm1
	movaps	[rsp + 90h], xmm2
	movaps	[rsp + 80h], xmm3
	movaps	[rsp + 70h], xmm4
	movaps	[rsp + 60h], xmm5
	mov		[rsp + 58h], rax
	mov		[rsp + 50h], rcx
	mov		[rsp + 48h], rdx
	mov		[rsp + 40h], r8
	mov		[rsp + 38h], r9
	mov		[rsp + 30h], r10
	mov		[rsp + 28h], r11
	mov		[rsp + 20h], r15
endm

FUNC_EPILOGUE macro
	mov		r15, [rsp + 20h]
	mov		r11, [rsp + 28h]
	mov		r10, [rsp + 30h]
	mov		r9, [rsp + 38h]
	mov		r8, [rsp + 40h]
	mov		rdx, [rsp + 48h]
	mov		rcx, [rsp + 50h]
	mov		rax, [rsp + 58h]
	movaps	xmm5, [rsp + 60h]
	movaps	xmm4, [rsp + 70h]
	movaps	xmm3, [rsp + 80h]
	movaps	xmm2, [rsp + 90h]
	movaps	xmm1, [rsp + 0A0h]
	movaps	xmm0, [rsp + 0B0h]
	mov		rsp, r15
	pop		r15
endm


EXTERN mp_zone_changing_injection_return: qword
extern mp_zone_changing_helper: proc

; Assembly code below is injected into the function that changes the the current mp zone
PUBLIC mp_zone_changing_injection
mp_zone_changing_injection PROC

;original code
;preventivly write the zones
mov     [rbx+354h], eax
mov     rcx, rbx
mov     eax, [rdi+0Ch]
mov     [rbx+358h], eax

FUNC_PROLOGUE
mov     ecx, dword ptr [rbx+354h] ;the vanilla zone
mov     rdx, rbx ;the current entity
call    mp_zone_changing_helper
FUNC_EPILOGUE

jmp     mp_zone_changing_injection_return

mp_zone_changing_injection ENDP

_TEXT    ENDS

END
